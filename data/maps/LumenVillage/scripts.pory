mapscripts LumenVillage_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        1, 1 {
            turnobject(LOCALID_PLAYER, DIR_EAST)
            if (!flag(FLAG_HIDE_CLEARING_JIRACHI)) {
                playbgm(MUS_NONE, 1)
            }
        }
    ]

    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_INTRO_SCENE, 1 {
            setvar(VAR_INTRO_SCENE, 2)
            addobject(LumenVillage_Rival_Intro)
            applymovement(LumenVillage_Rival_Intro, moves(emote_exclamation_mark))
            playse(SE_PIN)
            waitmovement(0)
            playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
            delay(50)
            applymovement(LumenVillage_Rival_Intro, moves(walk_fast_left *3
                                                        walk_fast_up * 3))
            waitmovement(0)
            call(RivalName)
            msgbox(format("Hey, {PLAYER}! What are you doing out here?\l"
                "WHOA - hold up! You've got a Pokémon?!\l"
                "Wait a second... you SERIOUSLY already got a Pokémon??\l"
                "I can't believe this! After all these years training together, and you didn't even tell me??\l"
                "Tch... classic {PLAYER}. Always one step ahead, huh?\l"
                "Well, guess what - I'm not letting you leave me in the dust!\l"
                "I'll get my own Pokémon too, you'll see!"), MSGBOX_AUTOCLOSE)
            applymovement(LumenVillage_Rival_Intro, moves(walk_fast_down * 8))
            waitmovement(0)
            removeobject(LumenVillage_Rival_Intro)
            fadedefaultbgm()
            end
        }
    ]
}

script LumenVillage_EventScript_GoBack_LeftTrigger {
	if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
		lockall
		msgbox("You can't ignore the cries of\n"
				"a hurt Pokémon!")
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_right))
		waitmovement(0)
		releaseall
	}
}

script LumenVillage_EventScript_GoBack_UpTrigger {
	if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
		lockall
		msgbox("You can't ignore the cries of\n"
				"a hurt Pokémon!")
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_down))
		waitmovement(0)
		releaseall
	}
}

script LumenVillage_EventScript_SetIntroFlags {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        call(LumenVillage_EventScript_SetIntroFlags_Male)
    } else {
        call(LumenVillage_EventScript_SetIntroFlags_Female)
    }
}

script LumenVillage_EventScript_SetIntroFlags_Male {
    setrespawn(HEAL_LOCATION_LUMEN_VILLAGE)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_CASSIANS_HOUSE_MOM)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_SELENES_HOUSE_RIVAL_MOM)
    addobject(Cassians_House_Mom)
    addobject(Selenes_House_Rival_Mom)
    releaseall
}

script LumenVillage_EventScript_SetIntroFlags_Female {
    setrespawn(HEAL_LOCATION_LUMEN_VILLAGE)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_SELENES_HOUSE_MOM)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_CASSIANS_HOUSE_RIVAL_MOM)
    addobject(Selenes_House_Mom)
    addobject(Cassians_House_Rival_Mom)
    releaseall
}

script LumenVillage_EventScript_ProfLabSign {
    lockall
    msgbox("Prof. Myra's Lab")
    releaseall
}

script LumenVillage_EventScript_SelenesHouseSign {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == FEMALE) {
        msgbox(LumenVillage_Text_PlayersHouse_Sign)
    } else {
        msgbox(format("Selene's House"))
    }
    releaseall
}

script LumenVillage_EventScript_CassiansHouseSign {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        msgbox(LumenVillage_Text_PlayersHouse_Sign)
    } else {
        msgbox(format("Cassian's House"))
    }
    releaseall
}

script LumenVillage_EventScript_VillageSign {
    lockall
    msgbox("Lumen Village\n"
        "“A quiet place beneath the stars”")
    releaseall
}

script LumenVillage_FatMan_Dialogue {
    applymovement(LumenVillage_FatMan, moves(face_player))
    msgbox(format("Technology is amazing!"
        "You can now use satellites to chart the stars in Astrea!"), type=MSGBOX_NPC)
}

script LumenVillage_OldLady_Dialogue {
    applymovement(LumenVillage_OldLady, moves(face_player))
    msgbox(format("Lumen Village must be the safest place in Astrea, but I swear I hear rustling in the night lately..."), type=MSGBOX_NPC)
}

script LumenVillage_LittleGirl_Dialogue {
    applymovement(LumenVillage_LittleGirl, moves(face_player))
    msgbox(format("The water looks so nice!\n"
                    "But I'm not allowed to swim yet..."), type=MSGBOX_NPC)
}

text LumenVillage_Text_PlayersHouse_Sign {
    format("{PLAYER}'s House")
}
mapscripts BeginnerClearing_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        1, 1 {
            removeobject(GruntA)
            removeobject(GruntB)
            removeobject(ProfMyra)
            removeobject(MyrasBag)
        }
    ]
}

movement BeginnerClearing_ExclamationMark {
    emote_exclamation_mark
}

movement BeginnerClearing_QuestionMark {
    emote_question_mark
}

movement BeginnerClearing_WalkUpToJirachiFromA {
    walk_fast_right * 5
    walk_fast_up * 2
    face_right
}

movement BeginnerClearing_WalkUpToJirachiFromB {
    walk_fast_right * 5
    walk_fast_up * 3
    face_right
}

movement BeginnerClearing_MyraRunsInA {
    walk_fast_right * 3
}

movement BeginnerClearing_MyraRunsInB {
    walk_fast_right * 6
    walk_fast_up * 1
}

movement BeginnerClearing_GruntAWalksIn {
    walk_fast_right * 4
}

movement BeginnerClearing_GruntBWalksIn {
    walk_fast_right * 5
}

movement BeginnerClearing_PlayerRunsInFrontOfGrunts {
    walk_fast_down * 2
    walk_fast_left
    face_left
}

movement BeginnerClearing_ProfMyraWalksToPlayer {
    walk_down * 2
    walk_left
}

movement BeginnerClearing_GruntARunsToPlayer {
    walk_fast_right * 2
}

script TestSound {
    lockall
    applymovement(test_npc, moves(face_player))
    msgbox("Does this play a sound?\l"
        "If it does, then the sound system is\nworking!\l"
        "If it doesn't, then something is wrong.\l"
        "This is a test message.")
    releaseall
}

script BeginnerClearing_EventScript_GoBack {
	if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
		lockall
		msgbox("You can't ignore the cries of\n"
				"a hurt Pokémon!")
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_right))
		waitmovement(0)
		releaseall
	}
}

script BeginnerClearing_EventScript_WalkFromA {
    lockall
    playmoncry(SPECIES_JIRACHI, CRY_MODE_NORMAL)
    waitmoncry()
    applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, BeginnerClearing_WalkUpToJirachiFromA)
    waitmovement(0)
    call(BeginnerClearing_EventScript_JirachiIntro)
    releaseall
}

script BeginnerClearing_EventScript_WalkFromB {
    lockall
    playmoncry(SPECIES_JIRACHI, CRY_MODE_NORMAL)
    waitmoncry()
    applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, BeginnerClearing_WalkUpToJirachiFromB)
    waitmovement(0)
    call(BeginnerClearing_EventScript_JirachiIntro)
    releaseall
}

script BeginnerClearing_EventScript_ProfMyra {
    msgbox("I will lend you one of my Pokémon so\nwe can defend this Jirachi together.\l"
        "Please grab a Pokéball from my bag.", type=MSGBOX_NPC)
}

script BeginnerClearing_EventScript_GruntA {
    msgbox(format("Fool! Hand Jirachi over and don't interfere!"), type=MSGBOX_NPC)
}

script BeginnerClearing_EventScript_GruntB {
    msgbox(format("Don't meddle where you aren't supposed to!"), type=MSGBOX_NPC)
}

script BeginnerClearing_EventScript_JirachiNPC {
    playmoncry(SPECIES_JIRACHI, CRY_MODE_NORMAL)
    waitmoncry()
}

script BeginnerClearing_EventScript_JirachiIntro {
    lockall
    msgbox(format("Jirachi: Help... they're coming after me..."))
    playse(SE_SELECT)
    delay(10)
    applymovement(LOCALID_PLAYER, BeginnerClearing_QuestionMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    msgbox(format("They took my friend... help..."))
    playse(SE_SELECT)
    addobject(ProfMyra)
    applymovement(ProfMyra, BeginnerClearing_MyraRunsInA)
    waitmovement(0)
    applymovement(ProfMyra, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(ProfMyra, BeginnerClearing_MyraRunsInB)
    waitmovement(0)
    addobject(MyrasBag)
    msgbox(format("Prof. Myra: Is that a Jirachi? Oh, it's hurt!"))
    playse(SE_SELECT)
    msgbox(format("Quick, I have a Potion!"))
    playse(SE_USE_ITEM)
    waitse()
    msgbox(format("It should be okay for now, but it needs rest.\l"
        "The poor thing's definitely in bad shape.\l"
        "I wonder what happened..."))
    playse(SE_SELECT)
    applymovement(ProfMyra, moves(face_left))
    waitmovement(0)
    msgbox(format("What's your name?\l"
        "...\l"
        "{PLAYER}, is it?\l"
        "We are lucky you found it so quickly.\l"
        "We should take it somewhere to recover.\l"
        "...\l"
        "You said Jirachi spoke to you? And\nthat it said it's being chased?"))
    playse(SE_SELECT)
    addobject(GruntA)
    addobject(GruntB)
    applymovement(GruntA, BeginnerClearing_GruntAWalksIn)
    applymovement(GruntB, BeginnerClearing_GruntBWalksIn)
    waitmovement(0)
    playbgm(MUS_RG_ROCKET_HIDEOUT, FALSE)
    delay(40)
    applymovement(GruntA, BeginnerClearing_ExclamationMark)
    applymovement(GruntB, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, moves(face_left))
    applymovement(ProfMyra, moves(face_left))
    msgbox(format("Grunt 1: Here it is! Grab it!"))
    playse(SE_SELECT)
    msgbox(format("Grunt 2: You! Hand over that Jirachi!"))
    playse(SE_SELECT)
    delay(10)
    applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
    applymovement(ProfMyra, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, BeginnerClearing_PlayerRunsInFrontOfGrunts)
    waitmovement(0)
    delay(50)
    msgbox(format("Grunt 1: Huh? Who are you?"))
    playse(SE_SELECT)
    msgbox(format("Grunt 2: You think a little dweeb like you can get in our way?"))
    playse(SE_SELECT)
    msgbox(format("Prof. Myra: And who are you? What do you want with this Jirachi?"))
    playse(SE_SELECT)
    msgbox(format("Grunt 2: That's none of your business, lady!"))
    playse(SE_SELECT)
    msgbox(format("Grunt 1: Just hand over the Jirachi and no one gets hurt!"))
    playse(SE_SELECT)
    applymovement(ProfMyra, BeginnerClearing_ProfMyraWalksToPlayer)
    waitmovement(0)
    msgbox(format("Prof. Myra: {PLAYER}, do you have any Pokémon?"))
    playse(SE_SELECT)
    msgbox(format("..."))
    playse(SE_SELECT)
    msgbox(format("Hmmm, I see. I think this situation\ncalls for special circumstances."))
    playse(SE_SELECT)
    msgbox(format("I will lend you one of my Pokémon so\nwe can defend this Jirachi together.\l"
        "Please grab a Pokéball from my bag."))
    playse(SE_SELECT)
    clearflag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)
    releaseall
}

movement BeginnerClearing_WalkToGruntA_LeftSideOfBag {
    walk_down
    walk_left * 4
}

movement BeginnerClearing_WalkToGruntA_TopSideOfBag {
    walk_left
    walk_down * 2
    walk_left * 4
}

movement BeginnerClearing_WalkToGruntA_RightSideOfBag {
    walk_down
    walk_left * 6
}

movement BeginnerClearing_WalkToGruntA_BottomSideOfBag {
    walk_left * 4
}

script BeginnerClearing_EventScript_MyrasBag {
    lockall
    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_SYS_POKEMON_GET)
    special(ChooseStarter)
    call(BeginnerClearing_EventScript_GruntABattle)
    releaseall
}

script BeginnerClearing_EventScript_GruntABattle {
    lockall
    applymovement(GruntA, BeginnerClearing_GruntARunsToPlayer)
    waitmovement(0)
    switch (var(VAR_FACING)) {
        case DIR_EAST: // facing east, player is left of bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_LeftSideOfBag)
        case DIR_SOUTH: // facing south, player is above bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_TopSideOfBag)
        case DIR_WEST: // facing west, player is right of bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_RightSideOfBag)
        case DIR_NORTH: // facing north, player is below bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_BottomSideOfBag)
    }
    waitmovement(LOCALID_PLAYER)
    trainerbattle_single(TRAINER_GRUNT_MAGMA_HIDEOUT_14, "You're going down, kid!", "Impossible...", FALSE)
    releaseall
}